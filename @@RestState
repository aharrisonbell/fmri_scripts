#!/bin/tcsh
# @RestState
# by AHB, started July 2012
set version = 1.02-08-2012
# Oxford Script

####################################################################################################
#       SET UP VARIABLES HERE*
#
# Directories
set nii_dir = $PWD/nii_files
set out_dir = $PWD/resting_state
set anat_dir = $PWD/structurals
set temp_dir = $PWD/temp
set scriptdir = ~/Documents/fMRI_Data/Oxford_Data/scripts # specifies location of TOPUP, etc.
#set scriptdir = ~/Desktop/scripts # specifies location of TOPUP, etc.
#
#
# Program Options
set REV_OPTION = "y"  # (y/n) - calls up FSLVIEW window after each step (USEFUL!!)
#
# incoming data labels (the script identifies input data by searching for these threads)
set epi_label = ep2dfmri
set fmap_label = fieldmapTEs
set epi_volume = `ls ${nii_dir}/*${epi_label}.nii*`
set fmap_volume = `ls ${nii_dir}/*${fmap_label}*2001.nii.gz|tail -1`
echo $epi_volume
echo $fmap_volume
#
# BET Variables (affects skull-stripping.  Play around with these values if you are having problems)
set betF = 0.5 # Suggested value: 0.5
set betG = 0.1 # Suggested values: 0.1, 0.2
#
# Standard Atlases
set mni_atlas = ~/Documents/fMRI_Data/Oxford_Data/DicomProcessed/mni_brain/mni_brain_strip.nii
set caret_atlas = ~/Documents/fMRI_Data/Oxford_Data/DicomProcessed/caret_brain/CaretBrain_skullstripped.nii.gz
#set mni_atlas = /imaging/ab03/mni_brain/mni_brain_strip.nii
#set caret_atlas = /imaging/ab03/caret_brain/CaretBrain_skullstripped.nii.gz
#
# *so non-coders, don't f**k with stuff below this section and then complain to me that
#  the code doesn't work :)
####################################################################################################

if ($#argv == 0) then
	echo " "
	echo "***********************************"
	echo "@RestState by AHB, V"$version
	echo "***********************************"
	echo " "
	echo "Script to preprocess/analyse EPI data collected at the BSB (currently only for resting state data)"
	echo "Note: this script assumes you have AFNI installed AND have already run @AnatScript to completion."
	echo "INPUT: @EPI_FC_Script <SESSION> <STEP>"
	echo " "
	echo "This script "
	echo "where:"
	echo " session_name = Label for data (e.g., MonkeyDate).  This has no impact on the program,"
	echo "                it simply labels the data in the directory."
	echo " step (optional): perform step X where..."
	echo "   1) FUGUE (field map correction)"
	echo "   2) Correct Alignment"
	echo "   3) Trim EPI dataset (remove dummy scans)"
	echo "   4) Skull strip EPI dataset using BET (need to correct manually)"
    	echo "   5) Motion Correction (MCFLIRT)"
	echo "   6) Slice Time Correction (SLICETIMER)"
	echo "   7) Smooth dataset (FSLMATHS)"
	echo "   8) Align EPI structural to ATLAS"
	echo "   9) Prepare directory and files for MATLAB/CONN"	
	echo "   Note: Default is to perform all steps."
	echo " "
	goto exit
endif

set session_name = $1
if ($#argv == 1) then
	set step = 0
else
	set step = $2
endif
mkdir -p $out_dir $temp_dir

###################################################################################################
# Step 1: Apply fieldmap correction to EPI data (fugue)
if ($step =~ *1* || $step == 0) then
	date
	echo "Step 1: Apply fieldmap correction to EPI data (fugue)..."
	fugue -i ${epi_volume} --dwell=0.71 --loadfmap=${fmap_volume} -u $out_dir/${session_name}rs_1Fc
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}rs_1Fc.* &
	endif
	echo "Step 1 - Done."
endif

##################################################################################################
# Step 2: Correct alignment of EPI data
if ($step =~ *2* || $step == 0) then
	date
	echo "Step 2: Correct alignment of EPI data..."

    	#fslswapdim $out_dir/${session_name}rs_1Fc.nii.gz x z y $out_dir/${session_name}rs_2FcCa.nii.gz
    	fslswapdim $epi_volume x z y $out_dir/${session_name}rs_2FcCa.nii.gz
	fslorient -deleteorient $out_dir/${session_name}rs_2FcCa.nii.gz 
	fslorient -setqformcode 1 $out_dir/${session_name}rs_2FcCa.nii.gz 
	
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}rs_2FcCa* &
	endif
	echo "Step 2 - Done."
endif

###################################################################################################
# Step 3: Trim EPI dataset (remove dummy scans)
if ($step =~ *3* || $step == 0) then
	date
	echo "Step 3: Trim EPI dataset (remove dummy scans)..."
	# This is a stupid way but...
	fslsplit $out_dir/${session_name}rs_2FcCa.nii.gz $temp_dir/temp_vol
	rm -f $temp_dir/temp_vol000[1-5]*
	fslmerge -t $out_dir/${session_name}rs_3FcCaTr $temp_dir/temp_vol* 
	#rm -f $temp_dir/temp_vol*
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}rs_3FcCaTr*nii.gz &
	endif
	echo "Step 3 - Done."
endif

###################################################################################################
# Step 4: Skull strip EPI dataset using BET (need to correct manually)
if ($step =~ *4* || $step == 0) then
	date
	# Automatic Skull-Stripping
	echo "Step 4: Skull strip EPI dataset using BET (need to correct manually)..."
	bet $out_dir/${session_name}rs_3FcCaTr $out_dir/${session_name}rs_4FcCaTrSs -f $betF -g $betG
	
	# Skull-Stripping using fixed mask structural
	flirt -in $anat_dir/${session_name}hr_2MgFx.nii.gz -ref $out_dir/${session_name}rs_2FcCa.nii.gz \
	-dof 9 -omat $out_dir/${session_name}_struct2epi.mat -out $out_dir/${session_name}_struct2epi
	flirt -in $anat_dir/${session_name}_anatmask_fix.nii.gz -ref $out_dir/${session_name}rs_2FcCa.nii.gz -applyxfm \
	-init $out_dir/${session_name}_struct2epi.mat -out $out_dir/${session_name}_structmask2epi
	fslmaths $out_dir/${session_name}rs_3FcCaTr.nii.gz -mas $out_dir/${session_name}_structmask2epi $out_dir/${session_name}rs_4FcCaTrSx
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}rs_3FcCaTr*nii.gz $out_dir/${session_name}_struct2epi*nii.gz &
		fslview $out_dir/${session_name}rs_3FcCaTr*.nii.gz $out_dir/${session_name}rs_4FcCaTrSs*nii.gz $out_dir/${session_name}rs_4FcCaTrSx*nii.gz &
	endif
	echo "Step 4 - Done."
endif

###################################################################################################
# Step 5: Motion correction (AFNI)
if ($step =~ *5* || $step == 0) then
	date
	echo "Step 5: Motion Correction..."
	mcflirt -in $out_dir/${session_name}rs_4FcCaTrSx.nii.gz -out $out_dir/${session_name}rs_5FcCaTrSxMc -stats -plots
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}rs_5FcCaTrSxMc.nii* &
	endif
	echo "Step 5 - Done - Be sure to double check output."
endif

###################################################################################################
# Step 6: Slice time correction
if ($step =~ *6* || $step == 0) then
	date
	echo "Step 6: Slice time correction..."
	slicetimer -i $out_dir/${session_name}rs_5FcCaTrSxMc.nii.gz -o $out_dir/${session_name}rs_6FcCaTrSxMcSt.nii.gz \
	-v -r 2 --odd -d 3
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}rs_6FcCaTrSxMcSt.* &
	endif
	echo "Step 6 - Done."
endif

###################################################################################################
# Step 7: Spatial Filtering
if ($step =~ *7* || $step == 0) then
	date
	echo "Sigma = FWHM / 2.355"
	echo "Step 7: Spatial filtering..."
	fslmaths $out_dir/${session_name}rs_6FcCaTrSxMcSt.nii.gz -s 1.273 $out_dir/${session_name}rs_7FcCaTrSxMcStSm 
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}rs_7FcCaTrSxMcStSm.* &
	endif
	echo "Step 7 - Done."
endif

###################################################################################################
# Step 8: Alignment to atlas
if ($step =~ *8* || $step == 0) then
	date
	echo "Step 8: Align skullstripped EPI to atlas..."
	flirt -in $out_dir/${session_name}rs_4FcCaTrSx.nii.gz -ref $anat_dir/${session_name}hr_StrippedAnat_native.nii.gz -out $out_dir/${session_name}rs_epi2struct -omat $out_dir/${session_name}rs_epi2struct.mat
	flirt -in $out_dir/${session_name}rs_4FcCaTrSx.nii.gz -ref $caret_atlas -out $out_dir/${session_name}rs_epi2atlas_auto 
	flirt -in $out_dir/${session_name}rs_epi2struct.nii.gz -ref $caret_atlas -applyxfm -init $anat_dir/${session_name}_struct2caret.mat -out $out_dir/${session_name}rs_epi2atlas
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $anat_dir/${session_name}hr_StrippedAnat_native.nii.gz $out_dir/${session_name}rs_epi2struct.nii.* &
		fslview $caret_atlas $anat_dir/${session_name}hr_StrippedAnat_caret.nii.gz $out_dir/${session_name}rs_epi2atlas*.nii* &
	endif
	echo "Step 8 - Done."
endif

###################################################################################################
# Step 9: Prep directory for CONN/Matlab
if ($step =~ *9* || $step == 0) then
	date
	echo "Step 9: Prep directory for CONN/Matlab..."
	echo "Conn toolbox requires the following files (all aligned to Caret Atlas:"
	echo " -- EPI aligned to CARET atlas"
	echo " -- normalized structural"
	echo " -- gray matter volume"
	echo " -- white matter volume"
	echo " -- CSF volume"
	echo " -- ROIS, aligned to Caret"
	
	mkdir -p $out_dir/conn_data
	rm -f $out_dir/conn_data/* $out_dir/conn_data/epi_split/* $out_dir/conn_data/epi_split_align/*
	echo "...copying structurals"
	cp $caret_atlas $out_dir/conn_data
	cp $anat_dir/${session_name}hr_StrippedAnat_caret.nii* $out_dir/conn_data/${session_name}_structural.nii.gz
	
	echo "...aligning and copying segmentation volumes"
	flirt -in $anat_dir/${session_name}hr_StrippedAnat_native_pve_0.nii.gz -ref $caret_atlas -applyxfm -init $anat_dir/${session_name}_struct2caret.mat -out $out_dir/conn_data/${session_name}_CSF.nii.gz 
	flirt -in $anat_dir/${session_name}hr_StrippedAnat_native_pve_1.nii.gz -ref $caret_atlas -applyxfm -init $anat_dir/${session_name}_struct2caret.mat -out $out_dir/conn_data/${session_name}_GreyMatter.nii.gz 
	flirt -in $anat_dir/${session_name}hr_StrippedAnat_native_pve_2.nii.gz -ref $caret_atlas -applyxfm -init $anat_dir/${session_name}_struct2caret.mat -out $out_dir/conn_data/${session_name}_WhiteMatter.nii.gz 
	flirt -in $anat_dir/${session_name}hr_StrippedAnat_native_pveseg.nii.gz -ref $caret_atlas -applyxfm -init $anat_dir/${session_name}_struct2caret.mat -out $out_dir/conn_data/${session_name}_Segmented.nii.gz 	
	
	echo "...splitting and aligning EPI data"
	mkdir -p $out_dir/conn_data/epi_split $out_dir/conn_data/epi_split_align
	fslsplit $out_dir/${session_name}rs_7FcCaTrSxMcStSm.nii.gz $out_dir/conn_data/epi_split/${session_name}rs_tr
 	convert_xfm -omat $out_dir/${session_name}_epi2caret.mat -concat $out_dir/${session_name}rs_epi2struct.mat $anat_dir/${session_name}_struct2caret.mat
	foreach tr (`seq -w 0 1600`)
		echo "......aligning volume "$tr
		flirt -in $out_dir/conn_data/epi_split/${session_name}rs_tr${tr}.nii.gz -ref $caret_atlas -applyxfm -init $out_dir/${session_name}_epi2caret.mat -out $out_dir/conn_data/epi_split_align/${session_name}rs_align_tr${tr}
	end
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/conn_data/${session_name}_*.nii.gz $out_dir/conn_data/epi_split_align/${session_name}rs_align_tr0000.nii.gz &
	endif
	
	echo "...Unzipping all volumes"
	gunzip $out_dir/conn_data/*.nii.gz
	gunzip $out_dir/conn_data/epi_split_align/*.nii.gz

	echo "Step 9 - Done."
endif

exit:
