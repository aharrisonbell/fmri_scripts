#!/bin/tcsh
# @AnatScript (aka @MakeAwesomeBrain)
# by AHB, Version 1-29.02.2012
# Script to process and segment structurals (for Caret, FreeSurfer, etc.)
# This script assumes that the data has already been converted from DICOMs and is in the proper
# orientation. If not, try using @Convert2nii.

####################################################################################################
#       SET UP VARIABLES HERE*
#
# Directories
set nii_dir = $PWD/nii_files
set out_dir = $PWD
set temp_dir = $PWD/temp
set scriptdir = ~/Documents/fMRI_Data/Oxford_Data/scripts # specifies location of TOPUP, etc.
#
# incoming data labels (the script identifies input data by searching for these threads)
set mpHR_label = mprage05x05x05mm1001.
set mpLR_label = mprage1x1x1mmnoIR1001
set LRvolume = `ls ${nii_dir}/*${mpLR_label}.nii*`
#
# BET Variables (affects skull-stripping.  Play around with these values if you are having problems)
set betF = 0.5 # Suggested value: 0.5
set betG = 0.1 # Suggested values: 0.1, 0.2
#
# Standard Atlases
set mni_atlas = ~/Documents/fMRI_Data/Oxford_Data/DicomProcessed/mni_brain/mni_brain_strip.nii
set caret_atlas = ~/Documents/fMRI_Data/Oxford_Data/DicomProcessed/caret_brain/CaretBrain_skullstripped.nii.gz
#

# Program Options
set REV_OPTION = "Y"  # (y/n) - calls up FSLVIEW window after each step (USEFUL!!)
set AFNI_OPTION = "n" # (y/n) - includes AFNI-based skull stripping.  Useful to compare
#
# *so non-coders, don't f**k with stuff below this section and then complain to me that
#  the code doesn't work :)
####################################################################################################

if ($#argv == 0) then
	echo " "
	echo "********************************"
	echo "@AnatScript by AHB, V.12.13.2011"
	echo "********************************"
	echo " "
	echo "Script for processing structral data.  This script operates with several default values"
	echo "for things like location of rawdata, volume_name, inclusion of HR/LR volumes, etc.  If"
	echo "the script doesn't work, try altering the default values (edit this script)."
	echo " "
	echo "INPUT: @AnatScript <SESSION> <STEP>"
	echo " "
	echo "This script "
	echo "where:"
	echo " SESSION = Label for data (e.g., MonkeyDate).  This has no impact on the program,"
	echo "           it simply labels the data in the directory."
	echo " STEP (optional): perform step X where..."
	echo "   1) Merge High-Res Volumes"
	echo "   2) Correct Orientation"
	echo "   3) Skull Stripping (be sure to manually fix mask)"
	echo "   4) Align Low-Res to High-Res"
	echo "   5) Divide HR by LR and mask output (be sure to manually fix mask)"
	echo "   6) Align to Standard Atlases"
	echo "   7) Segment Volume"
	echo " "
	echo "Note: Default is to perform all steps (leave STEP blank).  Otherwise, run each step one"
	echo "      at a time."
	goto exit
endif

set session_name = $1
set step = $2
if ($#argv == 1) then
	set step = 0
endif
mkdir $out_dir $temp_dir

# Step 2: Correct orientation of all files
if ($step == 2 || $step == 0 || $step == 123456) then
	date
	echo "Step 2: Correcting orientation..." 
	fslswapdim $out_dir/${session_name}_HRmerge.nii.gz x z y $out_dir/${session_name}_HRmerge_fix.nii.gz
	fslorient -deleteorient $out_dir/${session_name}_HRmerge_fix.nii.gz 
	fslorient -setqformcode 1 $out_dir/${session_name}_HRmerge_fix.nii.gz 
		
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}_HRmerge_fix.nii.gz &
		fslview $out_dir/${session_name}_LR_fix.nii.gz &
	endif
	echo "Step 2: Done.  Be sure to confirm the labels are correct and the capsule appears on the LEFT side"
endif

# Step 3: Skull Stripping
if ($step == 3 || $step == 0 || $step == 123456) then
	date
	echo "Step 3: Skull-stripping merged high-res volume..."
	bet $out_dir/${session_name}_HRmerge_fix $out_dir/${session_name}_HRstrip_fix -f $betF -g $betG
	
	# Create mask from stripped volume
	fslmaths $out_dir/${session_name}_HRstrip_fix.nii.gz -bin -s 2 -thr 0.5 -bin $out_dir/${session_name}_mask_rough
	# cp $out_dir/${session_name}_mask* ./DTI_data
	
	echo "...Stripped volume and mask created in "$out_dir
	echo "...Be sure to check output."
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}_HRmerge_fix.nii.gz $out_dir/${session_name}_HRstrip_fix.nii.gz $out_dir/${session_name}_mask_rough.nii.gz $out_dir/${session_name}_HRstrip_fix_AFNI.nii.gz &
	endif
	echo "Step 3: Done."
	echo "You must now manually fix the mask - and rename it: 'session_name'_mask_fix.nii.gz   e.g., MI00100_mask_fix.nii.gz"
endif

# Step 4: Align LOWRES TO HIGHRES
if ($step == 4 || $step == 0 || $step == 123456) then
	date
	echo "Step 4: Using FLIRT to align low-res volume to merged high-res volume..."
	set LRvolume = `ls ${nii_dir}/*${mpLR_label}.nii*`
	flirt -in $out_dir/${session_name}_LR_fix.nii.gz -ref $out_dir/${session_name}_HRmerge_fix.nii.gz -dof 6 -omat $out_dir/${session_name}_hr2lr.mat -out $out_dir/${session_name}_LRalign
	echo "...Aligned low-res volume created in "$out_dir
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $out_dir/${session_name}_HRmerge_fix.nii.gz $out_dir/${session_name}_LRalign.nii.gz &
	endif
	echo "Step 4: Done."
endif
 
# Step 5: Divide HR by LR and mask
if ($step == 5 || $step == 0 || $step == 123456) then
 	date
 	echo "Step 5: Dividing high-res merged volume by low-res volume and mask..." 
  	fslmaths $out_dir/${session_name}_HRmerge_fix -div $out_dir/${session_name}_LRalign $temp_dir/${session_name}_HRcorr -odt float
  	fslmaths $temp_dir/${session_name}_HRcorr -mas $out_dir/${session_name}_mask_fix $out_dir/${session_name}_HRstrip_corr
	cp $out_dir/${session_name}_mask_fix* ./DTI_data
  	echo "...Corrected merged volume created in "$temp_dir
	if ($REV_OPTION == "y"||$REV_OPTION == "Y") then
		fslview $out_dir/${session_name}_HRmerge_fix.nii.gz \
		$temp_dir/${session_name}_HRcorr.nii.gz \
		$out_dir/${session_name}_HRstrip_corr.nii.gz &
	endif
	echo "Step 5: Done."
endif

# Step 6: Align to Standard Atlases
if ($step == 6 || $step == 0 || $step == 123456) then
	date
	echo "Step 6: Align to Standard Atlases..." 
	flirt -in $out_dir/${session_name}_HRstrip_corr.nii.gz -ref $mni_atlas -dof 9 -omat $out_dir/${session_name}_Align2MNI.mat -out $out_dir/${session_name}_Align2MNI
	flirt -in $out_dir/${session_name}_HRstrip_corr.nii.gz -ref $caret_atlas -dof 9 -omat $out_dir/${session_name}_Align2Caret.mat -out $out_dir/${session_name}_Align2Caret
	echo "...Aligned volumes created in "$out_dir
	
#	if ($AFNI_OPTION == "y"||$AFNI_OPTION == "Y") then
#		echo "...Using AFNI to align to standard atlas..."
#		rm -f $out_dir/${session_name}_HRalign2Atlas*
#		3dAllineate -input $out_dir/${session_name}_HRcorrmaskAFNI.nii.gz -base $mni_atlas \
#		-1Dparam_save $out_dir/${session_name}_HRtoAtlas -prefix $out_dir/${session_name}_HRalignAtlas
#	endif
	
	if ($REV_OPTION == "y" || $REV_OPTION == "Y") then
		fslview $mni_atlas $out_dir/${session_name}_Align2MNI.nii.gz &
		fslview $caret_atlas $out_dir/${session_name}_Align2Caret.nii.gz &
		#fslview $mni_atlas $out_dir/${session_name}_HRalignAtlas* &
	endif
	echo "Step 6: Done."
endif

# Step 7: Segment Volume
if ($step == 7 || $step == 0) then
	date
	echo "Step 7: Segmenting MPRAGE_noInversion-corrected volume..." 
	fast $out_dir/${session_name}_HRstrip_corr
	if ($REV_OPTION == "y"||$REV_OPTION == "Y") then
		fslview $out_dir/${session_name}_HRstrip_corr* &
	endif
	echo "...Booyah."
	echo "Step 7: Done."
endif

exit:
